注: Copyright (C) 2025 武汉凹语言科技有限公司
注: SPDX-License-Identifier: MIT

引入 "从容读写"
引入 "读写接口"
引入 "字串转译"

结构·整数读者:
	牛马: *从容读写·扫描仪
	有错: 错误
完毕

函数·来个整数读者(读者: 读写接口·读者) => *整数读者:
	返回 &整数读者{
		牛马: 从容读写·来个扫描仪(读者),
		有错: 空,
	}
完毕

函数·整数读者·迭代器 => 函数 => (还有: 布尔, 键, 值: 整型):
	设定·计件: 整型
	返回 函数 => (还有: 布尔, 键, 值: 整型):
		如果 我的·有错 != 空:
			还有 = 假
			返回
		完毕

		如果 !我的·牛马·干活():
			还有 = 假
			返回
		完毕

		单词 := 我的·牛马·成果()
		值, 我的·有错 = 字串转译·字串到整数(单词)
		如果 我的·有错 != 空:
			还有 = 假
			返回
		完毕

		还有 = 真
		键 = 计件
		计件++

		返回
	完毕
完毕
